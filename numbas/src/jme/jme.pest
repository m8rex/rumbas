jme = {
    SOI
    ~ expression
    ~ EOI
}
WHITESPACE = _{ (" " | "\t" | "\r" | "\n")+ }

expression =  { term ~ (infix_operator ~ term)* }
term       =  { prefix_operator* ~ node ~ postfix_operator* }
node       = _{ grouped_expression | terminal } // TODO  implicit_multiplication | 
terminal = _{ function_application | ident | collection | literal }  
grouped_expression = { "(" ~ expression ~ ")" }

reserved = @{ (boolean | logic_binary_operator) ~ !ident_char }

literal = _{ boolean | range | number | string }
//constant = { infinity_constant | nan_constant | changeable_constant }
// TODO: to rust code side
//infinity_constant = { "infty" | "infinity" }
// TODO unicode infinity char
//nan_constant = { "nan" }
//changeable_constant = { e_constant | pi_constant | i_constant }
//e_constant = { "e" }
//i_constant = { "i" }
//pi_constant = { "pi" }
// TODO pi unicode

number = _{ broken_number | integer }
integer = { ("-")? ~ ASCII_NONZERO_DIGIT ~ ASCII_DIGIT* | "0" }
broken_part = { ASCII_DIGIT* }
broken_number = { integer ~ "." ~ broken_part }
boolean = { boolean_true | boolean_false }
boolean_true = { "true" }
boolean_false = { "false" }
string = { "\"" ~ non_double_quoted_character* ~ "\"" |  "'" ~ non_single_quoted_character* ~ "'" }
non_double_quoted_character = _{ !"\"" ~ ANY }
non_single_quoted_character = _{ !"'" ~ ANY }


//arithmetic_function = { "exp" }
//builtin_function = { arithmetic_function }
//function = { builtin_function | ident }
//annotated_function = { (annotation)* ~ function }
function_arguments = { expression ~ ("," ~ expression)* }
function_application = { ident ~ "(" ~ function_arguments ~ ")" }

//implicit_multiplication_grouped = { 
//	(grouped_expression ~ ( ident | number | grouped_expression))
//} 
//implicit_multiplication_ident = { 
//	((number | ident) ~ ident)
//} // TODO x y z etc
//implicit_multiplication = _{ 
//    implicit_multiplication_grouped | implicit_multiplication_ident  
//}

//annotated_variable = { (annotation)* ~ ident }
verb_annotation = { "verb:" }
op_annotation = { "op:" }
vector_annotation = { "vector:" | "v:" }
unit_annotation = { "unit:" }
dot_annotation = { "dot:" }
matrix_annotation = { "matrix:" | "m:" }
diff_annotation = { "diff:" }
degrees_annotation = { "degrees:" }
builtin_annotation = { verb_annotation | op_annotation | vector_annotation | unit_annotation | dot_annotation | matrix_annotation | diff_annotation | degrees_annotation }
other_annotation = { ASCII_ALPHA+ ~ ":" }
annotation = { (builtin_annotation | other_annotation) }
// TODO: other
ident_char = _{ ASCII_ALPHA | ASCII_DIGIT | "_" | "'" }
ident_name = @{ !reserved ~ ASCII_ALPHA ~ (ASCII_ALPHA | ASCII_DIGIT | "_")* ~ "'"* }
ident = { (annotation)* ~ ident_name }


collection = _{ dictionary | list }
dictionary = { "[" ~ dictionary_item ~ ("," ~ dictionary_item )* ~ "]" }
dictionary_key = { ident_name | string }
dictionary_item = { dictionary_key ~ ":" ~ expression }
list = { "[" ~ (expression ~ ("," ~ expression )*)? ~ "]" }

arithmetic_operator = _{ add | subtract | multiply | divide | power }
    add      = { "+" }
    subtract = { "-" }
    multiply = { "*" }
    divide   = { "/" }
    power    = { "^" }
relational_operator = { less_or_equal | not_equal | less | greater_or_equal | greater | equal | in_operator } 
    less_or_equal = _{ "<="  } 
    not_equal = _{ "<>" } 
    less = _{  "<" } 
    greater_or_equal = _{ ">=" } 
    greater = _{ ">" } 
    equal = _{ "=" } 
    in_operator = _{ "in" } 
and_operator = _{ "and" | "&&" | "&" }
or_operator = _{ "or" }
xor_operator = _{ "xor" }
implies_operator = _{ "implies" }
logic_binary_operator = { and_operator | or_operator | xor_operator | implies_operator }
infix_operator = _{ arithmetic_operator | relational_operator | logic_binary_operator }
range = { integer? ~ ".." ~ integer? }  
index_operator = { "[" ~ (string | range | integer | ident_name) ~ "]" } 
faculty_operator =  { "!" }
postfix_operator = _{ faculty_operator | index_operator }
not_operator = { "!" | "not" }
prefix_operator = _{ not_operator }
